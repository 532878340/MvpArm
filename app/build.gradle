apply plugin: 'com.android.application'

def cfg = rootProject.ext.android
def libraries = rootProject.ext.dependencies
android {
    compileSdkVersion cfg.compileSdk
    defaultConfig {
        applicationId cfg.appId
        minSdkVersion cfg.minSdk
        targetSdkVersion cfg.targetSdk
        versionCode cfg.versionCode
        versionName cfg.versionName

        if(!isPlugin.toBoolean()){
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [moduleName: project.getName()]
                }
            }
        }
    }

    signingConfigs {
        config {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(STORE_FILE_PATH)
            storePassword STORE_PASSWORD
        }
    }

    sourceSets{
        main{
            if(isPlugin.toBoolean()){
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }else{
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下则需要排除debug文件夹中的所有Java文件
                java{
                    exclude 'debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.config

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //启用multidex的支持
            multiDexEnabled true
            //压缩对齐生成的apk包
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"
    productFlavors {
        yingyongbao{dimension "default"}
        huawei{dimension "default"}
        xiaomi{dimension "default"}

        //批量处理
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(':commonmodule')

    if(!isPlugin.toBoolean()){
        annotationProcessor "com.alibaba:arouter-compiler:${libraries.arouter_compile}"

        api project(':mainmodule')
        api project(':mallmodule')
    }
}
